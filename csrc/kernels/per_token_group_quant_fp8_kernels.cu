// SPDX-License-Identifier: MIT

#include "dispatch_utils.h"
#include "hip_compat.h"

#include <ATen/hip/HIPContext.h>
#include <ATen/hip/impl/HIPGuardImplMasqueradingAsCUDA.h>
#include <c10/cuda/CUDAMathCompat.h>
#include <torch/extension.h>

namespace aiter {

template <typename T, int N>
struct alignas(sizeof(T) * N) aligned_vector {
    T val[N];
};

inline __device__ uint8_t float_to_fp8e5m2(float f)
{
    constexpr uint32_t fp32_inf    = UINT32_C(255) << 23;
    constexpr uint32_t fp8_max     = UINT32_C(143) << 23;
    constexpr uint32_t denorm_mask = UINT32_C(134) << 23;
    uint32_t f_bits                = c10::detail::fp32_to_bits(f);
    uint8_t result                 = 0u;
    const uint32_t sign            = f_bits & UINT32_C(0x80000000);
    f_bits ^= sign;
    if(f_bits >= fp8_max)
    {
        result = f_bits > fp32_inf ? UINT8_C(0x7F) : UINT8_C(0x7C);
    }
    else
    {
        if(f_bits < (UINT32_C(113) << 23))
        {
            f_bits = c10::detail::fp32_to_bits(c10::detail::fp32_from_bits(f_bits) +
                                               c10::detail::fp32_from_bits(denorm_mask));
            result = static_cast<uint8_t>(f_bits - denorm_mask);
        }
        else
        {
            uint32_t mant_odd = (f_bits >> 21) & 1;
            f_bits += ((uint32_t)(15 - 127) << 23) + 0xFFFFF;
            f_bits += mant_odd;
            result = static_cast<uint8_t>(f_bits >> 21);
        }
    }
    result |= static_cast<uint8_t>(sign >> 24);
    return result;
}

inline __device__ uint8_t float_to_fp8e4m3(float f)
{
    constexpr uint32_t fp8_max     = UINT32_C(1087) << 20;
    constexpr uint32_t denorm_mask = UINT32_C(141) << 23;

    uint32_t f_bits     = c10::detail::fp32_to_bits(f);
    uint8_t result      = 0u;
    const uint32_t sign = f_bits & UINT32_C(0x80000000);
    f_bits ^= sign;

    if(f_bits >= fp8_max)
    {
        result = 0x7f;
    }
    else
    {
        if(f_bits < (UINT32_C(121) << 23))
        {
            f_bits = c10::detail::fp32_to_bits(c10::detail::fp32_from_bits(f_bits) +
                                               c10::detail::fp32_from_bits(denorm_mask));
            result = static_cast<uint8_t>(f_bits - denorm_mask);
        }
        else
        {
            uint8_t mant_odd = (f_bits >> 20) & 1;
            f_bits += ((uint32_t)(7 - 127) << 23) + 0x7FFFF;
            f_bits += mant_odd;
            result = static_cast<uint8_t>(f_bits >> 20);
        }
    }
    result |= static_cast<uint8_t>(sign >> 24);
    return result;
}

inline __device__ uint32_t float_to_fp8e4m3_x4(float a, float b, float c, float d)
{
#if defined(__gfx938__)
    int32_t res;
    res = __builtin_hcu_cvt_pk_fp8_f32(a, b, res, false);
    res = __builtin_hcu_cvt_pk_fp8_f32(c, d, res, true);
    return res;
#else
    int32_t out;
    reinterpret_cast<uint8_t*>(&out)[0] = float_to_fp8e4m3(a);
    reinterpret_cast<uint8_t*>(&out)[1] = float_to_fp8e4m3(b);
    reinterpret_cast<uint8_t*>(&out)[2] = float_to_fp8e4m3(c);
    reinterpret_cast<uint8_t*>(&out)[3] = float_to_fp8e4m3(d);
    return out;
#endif
}

inline __device__ uint32_t float_to_fp8e5m2_x4(float a, float b, float c, float d)
{
#if defined(__gfx938__)
    int32_t res;
    res = __builtin_hcu_cvt_pk_bf8_f32(a, b, res, false);
    res = __builtin_hcu_cvt_pk_bf8_f32(c, d, res, true);
    return res;
#else
    int32_t out;
    reinterpret_cast<uint8_t*>(&out)[0] = float_to_fp8e5m2(a);
    reinterpret_cast<uint8_t*>(&out)[1] = float_to_fp8e5m2(b);
    reinterpret_cast<uint8_t*>(&out)[2] = float_to_fp8e5m2(c);
    reinterpret_cast<uint8_t*>(&out)[3] = float_to_fp8e5m2(d);
    return out;
#endif
}

template <typename T, int reducesize = WARP_SIZE>
__inline__ __device__ T WarpReduceMax_ROW(T val)
{
#pragma unroll
    for(int offset = reducesize / 2; offset > 0; offset >>= 1)
    {
        val = fmaxf(val, VLLM_SHFL_DOWN_SYNC(val, offset));
    }
    return val;
}

template <typename T, int block_size = 512>
__inline__ __device__ T BlockReduceMax_ROW(T val, T* shared)
{
    constexpr int share_size = block_size / WARP_SIZE;
    val                      = WarpReduceMax_ROW<T>(val);
    if constexpr(block_size == WARP_SIZE)
    {
        return val;
    }
    else
    {
        const int lid = threadIdx.x % WARP_SIZE;
        const int wid = threadIdx.x / WARP_SIZE;
        if(lid == 0 && wid < share_size)
        {
            shared[wid] = val;
        }
        __syncthreads();
        if(wid == 0 && lid < share_size)
        {
            val = WarpReduceMax_ROW<T, share_size>(shared[lid]);
        }
        return val;
    }
}

template <int THREADS_PER_GROUP>
__inline__ __device__ float WarpGroupReduceMax(float val)
{
#pragma unroll
    for(int mask = THREADS_PER_GROUP / 2; mask > 0; mask >>= 1)
    {
        val = fmaxf(val, VLLM_SHFL_XOR_SYNC(val, mask));
    }
    return val;
}

template <typename scalar_t, int THREADS_PER_GROUP, bool is_e4m3>
__global__ void per_token_group_quant_fp8_small_kernel(const scalar_t* __restrict__ input,
                                                         uint8_t* __restrict__ output,
                                                         float* scale_out,
                                                         const int group_size,
                                                         const float eps,
                                                         const bool use_ue8m0,
                                                         const int64_t total_groups)
{
    constexpr int VEC = 16;

    int global_tid      = blockIdx.x * blockDim.x + threadIdx.x;
    int global_group_id = global_tid / THREADS_PER_GROUP;
    int lane_in_group   = global_tid % THREADS_PER_GROUP;

    if(global_group_id >= total_groups)
        return;

    using VecType    = aligned_vector<scalar_t, VEC>;
    using VecFp8Type = aligned_vector<uint8_t, VEC>;

    int64_t idx = static_cast<int64_t>(global_group_id) * group_size + lane_in_group * VEC;

    scalar_t input_vec[VEC];
    *(VecType*)input_vec = *(VecType*)(input + idx);

    float input_float_vec[VEC];
    float max_val = 0.f;

#pragma unroll
    for(int ii = 0; ii < VEC; ii++)
    {
        input_float_vec[ii] = static_cast<float>(input_vec[ii]);
        max_val             = fmaxf(max_val, fabsf(input_float_vec[ii]));
    }

    max_val = WarpGroupReduceMax<THREADS_PER_GROUP>(max_val);

    constexpr float FP8_MAX_VAL = is_e4m3 ? 448.0f : 57344.0f;
    max_val                     = fmaxf(max_val, eps);
    float scale_raw             = max_val / FP8_MAX_VAL;

    float s_group_scale;
    if(use_ue8m0)
    {
        s_group_scale = exp2f(ceilf(log2f(scale_raw)));
    }
    else
    {
        s_group_scale = scale_raw;
    }

    if(lane_in_group == 0)
    {
        scale_out[global_group_id] = s_group_scale;
    }

    float inv_s = (s_group_scale == 0.f) ? 0.f : 1.0f / s_group_scale;

    uint8_t out_vec[VEC];
#pragma unroll
    for(int ii = 0; ii < VEC; ii += 4)
    {
        uint32_t packed;
        if constexpr(is_e4m3)
        {
            packed = float_to_fp8e4m3_x4(input_float_vec[ii] * inv_s,
                                         input_float_vec[ii + 1] * inv_s,
                                         input_float_vec[ii + 2] * inv_s,
                                         input_float_vec[ii + 3] * inv_s);
        }
        else
        {
            packed = float_to_fp8e5m2_x4(input_float_vec[ii] * inv_s,
                                           input_float_vec[ii + 1] * inv_s,
                                           input_float_vec[ii + 2] * inv_s,
                                           input_float_vec[ii + 3] * inv_s);
        }
        *(uint32_t*)(&out_vec[ii]) = packed;
    }

    *(VecFp8Type*)(output + idx) = *(VecFp8Type*)out_vec;
}

template <typename scalar_t, int block_size, bool is_e4m3>
__global__ void per_token_group_quant_fp8_kernel(const scalar_t* __restrict__ input,
                                                 uint8_t* __restrict__ output,
                                                 float* scale_out,
                                                 const int group_size,
                                                 const float eps,
                                                 const bool use_ue8m0)
{
    constexpr int VEC = 16;
    int bid           = blockIdx.x;

    constexpr int share_size = block_size / WARP_SIZE;
    __shared__ float val_shared[share_size];
    __shared__ float s_group_scale;

    using VecType    = aligned_vector<scalar_t, VEC>;
    using VecFp8Type = aligned_vector<uint8_t, VEC>;

    int tid  = threadIdx.x;
    int tcol = group_size / VEC;

    int64_t idx = static_cast<int64_t>(bid) * tcol + tid;
    idx *= VEC;

    float input_float_vec[VEC];
    float max_val = 0.f;
    if(tid < tcol)
    {
        scalar_t input_vec[VEC];
        *(VecType*)input_vec = *(VecType*)(input + idx);
#pragma unroll
        for(int ii = 0; ii < VEC; ii++)
        {
            input_float_vec[ii] = static_cast<float>(input_vec[ii]);
            max_val             = fmaxf(max_val, fabsf(input_float_vec[ii]));
        }
    }

    max_val = BlockReduceMax_ROW<float, block_size>(max_val, val_shared);
    constexpr float FP8_MAX_VAL = is_e4m3 ? 448.0f : 57344.0f;

    if(tid == 0)
    {
        max_val = fmaxf(max_val, eps);
        float scale_raw = max_val / FP8_MAX_VAL;
        if(use_ue8m0)
        {
            s_group_scale = exp2f(ceilf(log2f(scale_raw)));
        }
        else
        {
            s_group_scale = scale_raw;
        }
        scale_out[bid] = s_group_scale;
    }
    __syncthreads();

    float inv_s = (s_group_scale == 0.f) ? 0.f : 1.0f / s_group_scale;

    uint8_t out_vec[VEC];
    if(tid < tcol)
    {
#pragma unroll
        for(int ii = 0; ii < VEC; ii += 4)
        {
            uint32_t packed;
            if constexpr(is_e4m3)
            {
                packed = float_to_fp8e4m3_x4(input_float_vec[ii] * inv_s,
                                             input_float_vec[ii + 1] * inv_s,
                                             input_float_vec[ii + 2] * inv_s,
                                             input_float_vec[ii + 3] * inv_s);
            }
            else
            {
                packed = float_to_fp8e5m2_x4(input_float_vec[ii] * inv_s,
                                               input_float_vec[ii + 1] * inv_s,
                                               input_float_vec[ii + 2] * inv_s,
                                               input_float_vec[ii + 3] * inv_s);
            }
            *(uint32_t*)(&out_vec[ii]) = packed;
        }
        *(VecFp8Type*)(output + idx) = *(VecFp8Type*)out_vec;
    }
}

template <typename scalar_t, bool is_e4m3>
void launch_per_token_group_quant_fp8_small(
    const scalar_t* input,
    uint8_t* out_ptr,
    float* scales_ptr,
    int group_size,
    float eps,
    bool use_ue8m0,
    int64_t num_groups,
    int threads_per_group,
    hipStream_t stream)
{
    constexpr int BLOCK_SIZE   = 256;
    const int groups_per_block = BLOCK_SIZE / threads_per_group;
    const int num_blocks =
        static_cast<int>((num_groups + groups_per_block - 1) / groups_per_block);

    switch(threads_per_group)
    {
    case 1:
        per_token_group_quant_fp8_small_kernel<scalar_t, 1, is_e4m3>
            <<<num_blocks, BLOCK_SIZE, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0, num_groups);
        break;
    case 2:
        per_token_group_quant_fp8_small_kernel<scalar_t, 2, is_e4m3>
            <<<num_blocks, BLOCK_SIZE, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0, num_groups);
        break;
    case 4:
        per_token_group_quant_fp8_small_kernel<scalar_t, 4, is_e4m3>
            <<<num_blocks, BLOCK_SIZE, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0, num_groups);
        break;
    case 8:
        per_token_group_quant_fp8_small_kernel<scalar_t, 8, is_e4m3>
            <<<num_blocks, BLOCK_SIZE, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0, num_groups);
        break;
    case 16:
        per_token_group_quant_fp8_small_kernel<scalar_t, 16, is_e4m3>
            <<<num_blocks, BLOCK_SIZE, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0, num_groups);
        break;
    case 32:
        per_token_group_quant_fp8_small_kernel<scalar_t, 32, is_e4m3>
            <<<num_blocks, BLOCK_SIZE, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0, num_groups);
        break;
    default:
        TORCH_CHECK(false, "Unsupported threads_per_group for small kernel: ", threads_per_group);
    }
}

template <typename scalar_t, bool is_e4m3>
void launch_per_token_group_quant_fp8_large(const scalar_t* input,
                                            uint8_t* out_ptr,
                                            float* scales_ptr,
                                            int group_size,
                                            float eps,
                                            bool use_ue8m0,
                                            int64_t num_groups,
                                            int block_size,
                                            hipStream_t stream)
{
    switch(block_size)
    {
    case 64:
        per_token_group_quant_fp8_kernel<scalar_t, 64, is_e4m3>
            <<<num_groups, 64, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0);
        break;
    case 128:
        per_token_group_quant_fp8_kernel<scalar_t, 128, is_e4m3>
            <<<num_groups, 128, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0);
        break;
    case 256:
        per_token_group_quant_fp8_kernel<scalar_t, 256, is_e4m3>
            <<<num_groups, 256, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0);
        break;
    case 512:
        per_token_group_quant_fp8_kernel<scalar_t, 512, is_e4m3>
            <<<num_groups, 512, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0);
        break;
    case 1024:
        per_token_group_quant_fp8_kernel<scalar_t, 1024, is_e4m3>
            <<<num_groups, 1024, 0, stream>>>(
                input, out_ptr, scales_ptr, group_size, eps, use_ue8m0);
        break;
    default:
        TORCH_CHECK(false, "Unsupported block_size for large kernel: ", block_size);
    }
}

void per_token_group_quant_fp8(torch::Tensor& out,
                               torch::Tensor input,
                               torch::Tensor& scales,
                               int64_t group_size,
                               float eps,
                               bool use_ue8m0)
{
    input = input.is_contiguous() ? input : input.contiguous();
    TORCH_CHECK(out.is_contiguous(), "out must be contiguous");
    TORCH_CHECK(scales.is_contiguous(), "scales must be contiguous");
    TORCH_CHECK(out.scalar_type() == torch::kFloat8_e4m3fn ||
                    out.scalar_type() == torch::kFloat8_e5m2,
                "Output tensor must be float8_e4m3fn or float8_e5m2");

    const bool is_e4m3        = (out.scalar_type() == torch::kFloat8_e4m3fn);
    const int hidden_size     = static_cast<int>(input.size(-1));
    const int group_size_int  = static_cast<int>(group_size);

    TORCH_CHECK(hidden_size % group_size_int == 0,
                "hidden_size must be a multiple of group_size");
    TORCH_CHECK(group_size_int <= 16384, "group_size exceeds maximum supported size of 16384");
    TORCH_CHECK(group_size_int >= 16 && group_size_int % 16 == 0,
                "group_size must be >=16 and a multiple of 16");
    TORCH_CHECK((group_size_int & (group_size_int - 1)) == 0,
                "group_size must be a power of 2");

    const int64_t num_groups = input.numel() / group_size_int;
    const at::hip::OptionalHIPGuardMasqueradingAsCUDA device_guard(device_of(input));
    const hipStream_t stream = at::hip::getCurrentHIPStream();
    auto out_ptr             = reinterpret_cast<uint8_t*>(out.data_ptr());

    VLLM_DISPATCH_FLOATING_TYPES(input.scalar_type(), "per_token_group_quant_fp8_kernel", [&] {
        const scalar_t* input_ptr = input.data_ptr<scalar_t>();
        float* scales_ptr         = scales.data_ptr<float>();
        if(group_size_int < 1024)
        {
            const int threads_per_group = group_size_int / 16;
            if(is_e4m3)
            {
                launch_per_token_group_quant_fp8_small<scalar_t, true>(
                    input_ptr, out_ptr, scales_ptr, group_size_int, eps, use_ue8m0,
                    num_groups, threads_per_group, stream);
            }
            else
            {
                launch_per_token_group_quant_fp8_small<scalar_t, false>(
                    input_ptr, out_ptr, scales_ptr, group_size_int, eps, use_ue8m0,
                    num_groups, threads_per_group, stream);
            }
        }
        else
        {
            const int block_size = group_size_int / 16;
            if(is_e4m3)
            {
                launch_per_token_group_quant_fp8_large<scalar_t, true>(
                    input_ptr, out_ptr, scales_ptr, group_size_int, eps, use_ue8m0,
                    num_groups, block_size, stream);
            }
            else
            {
                launch_per_token_group_quant_fp8_large<scalar_t, false>(
                    input_ptr, out_ptr, scales_ptr, group_size_int, eps, use_ue8m0,
                    num_groups, block_size, stream);
            }
        }
    });
}

} // namespace aiter
