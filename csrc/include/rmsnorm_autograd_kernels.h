// SPDX-License-Identifier: MIT
#pragma once

#include "rmsnorm_autograd_reduce.h"

namespace aiter {
namespace rmsnorm_autograd {

template <typename scalar_t>
__global__ void GammaBackward(int M,
                              int N,
                              const scalar_t* dY,
                              const scalar_t* X,
                              const float* rstd,
                              scalar_t* dg)
{
    using T_ACC = at::acc_type<scalar_t, true>;
    __shared__ T_ACC g_shared[kColwiseReduceTileSize][kColwiseReduceTileSize + 1];
    const int j   = blockIdx.x * blockDim.x + threadIdx.x;
    T_ACC dg_sum  = 0;
    if(j < N)
    {
        for(int i = threadIdx.y; i < M; i += blockDim.y)
        {
            const int index = i * N + j;
            dg_sum += static_cast<T_ACC>(dY[index]) * static_cast<T_ACC>(X[index]) * rstd[i];
        }
    }
    g_shared[threadIdx.y][threadIdx.x] = dg_sum;
    __syncthreads();
    T_ACC sum1 = g_shared[threadIdx.x][threadIdx.y];
    sum1       = WarpReduceSum<T_ACC, 16>(sum1);
    if(threadIdx.x == 0)
    {
        const int jj = blockIdx.x * blockDim.x + threadIdx.y;
        if(jj < N)
        {
            dg[jj] = sum1;
        }
    }
}

template <typename scalar_t, typename T_ACC>
__global__ void g_backward_kernel_part_a(int M,
                                           int N,
                                           const scalar_t* dY,
                                           const scalar_t* X,
                                           const float* rstd,
                                           T_ACC* dg)
{
    __shared__ T_ACC g_shared[kColwiseReduceTileSize][kColwiseReduceTileSize + 1];
    const int j      = blockIdx.x * blockDim.x + threadIdx.x;
    T_ACC dg_sum     = 0;
    const int bidy   = blockIdx.y;
    const int gdimy  = gridDim.y;
    int col_block    = (M - 1) / gdimy + 1;
    int off          = col_block * blockIdx.y;
    if(bidy == gdimy - 1)
    {
        col_block = M - off;
    }
    if(j < N)
    {
        for(int i = threadIdx.y; i < col_block; i += blockDim.y)
        {
            const int i2    = i + off;
            const int index = i2 * N + j;
            dg_sum += static_cast<T_ACC>(dY[index]) * static_cast<T_ACC>(X[index]) * rstd[i2];
        }
    }
    g_shared[threadIdx.y][threadIdx.x] = dg_sum;
    __syncthreads();
    T_ACC sum1 = g_shared[threadIdx.x][threadIdx.y];
    sum1       = WarpReduceSum<T_ACC, 16>(sum1);
    if(threadIdx.x == 0)
    {
        const int jj = blockIdx.x * blockDim.x + threadIdx.y;
        if(jj < N)
        {
            dg[jj * gdimy + bidy] = sum1;
        }
    }
}

template <typename scalar_t, typename T_ACC>
__global__ void g_backward_kernel_part_b(const T_ACC* part_grad_gamma, scalar_t* dg)
{
    int idx   = blockIdx.x * blockDim.x + threadIdx.x;
    T_ACC gamma = part_grad_gamma[idx];
    gamma       = WarpReduceSum(gamma);
    if(threadIdx.x == 0)
    {
        dg[blockIdx.x] = gamma;
    }
}

} // namespace rmsnorm_autograd
} // namespace aiter

#define RMSNORMBACKWARD_VEC_2048                                                                 \
    using T_ACC = at::acc_type<scalar_t, true>;                                                      \
    __shared__ T_ACC ds_shared[16];                                                              \
    __shared__ T_ACC b;                                                                          \
    const int i = blockIdx.x;                                                                    \
    const int j = threadIdx.x;                                                                   \
    const int index = i * N + j * VEC;                                                           \
    using VecTpye = aiter::rmsnorm_autograd::aligned_vector<scalar_t, VEC>;                                       \
    T_ACC sum1 = 0;                                                                              \
    int tcol = N / VEC;                                                                          \
    VecTpye tx;                                                                                  \
    VecTpye tdy;                                                                                 \
    VecTpye tdx;                                                                                 \
    VecTpye tgamma;                                                                              \
    scalar_t* px    = (scalar_t*)(&tx);                                                          \
    scalar_t* pdy   = (scalar_t*)(&tdy);                                                         \
    scalar_t* pdx   = (scalar_t*)(&tdx);                                                         \
    scalar_t* pgamma = (scalar_t*)(&tgamma);                                                     \
    if(j < tcol)                                                                                 \
    {                                                                                            \
        tx     = *(VecTpye*)(X + index);                                                         \
        tdy    = *(VecTpye*)(dY + index);                                                        \
        tgamma = *(VecTpye*)(gamma + j * VEC);                                                   \
        for(int ii = 0; ii < VEC; ii++)                                                          \
        {                                                                                        \
            sum1 += static_cast<T_ACC>(pdy[ii]) * static_cast<T_ACC>(px[ii]) * pgamma[ii];      \
        }                                                                                        \
    }                                                                                            \
    sum1 = aiter::rmsnorm_autograd::BlockReduceSum<T_ACC>(sum1, ds_shared);                        \
    const T_ACC s = T_ACC(1) / static_cast<T_ACC>(N);                                            \
    float trstd   = rstd[i];                                                                     \
    if(threadIdx.x == 0)                                                                         \
    {                                                                                            \
        b = -sum1 * trstd * trstd * trstd * s;                                                   \
    }                                                                                            \
    __syncthreads();

#define RMSNORMBACKWARD_VEC_4096                                                                 \
    using T_ACC = at::acc_type<scalar_t, true>;                                                      \
    __shared__ T_ACC ds_shared[16];                                                              \
    __shared__ T_ACC b;                                                                          \
    const int i       = blockIdx.x;                                                              \
    const int j       = threadIdx.x;                                                             \
    const int index   = i * N + j * VEC;                                                         \
    const int index_2 = i * N + j * VEC + 2048;                                                  \
    using VecTpye = aiter::rmsnorm_autograd::aligned_vector<scalar_t, VEC>;                                       \
    T_ACC sum1 = 0;                                                                              \
    int tcol   = N / VEC;                                                                        \
    VecTpye tx;                                                                                  \
    VecTpye tdy;                                                                                 \
    VecTpye tdx;                                                                                 \
    VecTpye tgamma;                                                                              \
    VecTpye tx_2;                                                                                \
    VecTpye tdy_2;                                                                               \
    VecTpye tgamma_2;                                                                            \
    scalar_t* px       = (scalar_t*)(&tx);                                                       \
    scalar_t* pdy      = (scalar_t*)(&tdy);                                                      \
    scalar_t* pdx      = (scalar_t*)(&tdx);                                                      \
    scalar_t* pgamma   = (scalar_t*)(&tgamma);                                                   \
    scalar_t* px_2     = (scalar_t*)(&tx_2);                                                     \
    scalar_t* pdy_2    = (scalar_t*)(&tdy_2);                                                    \
    scalar_t* pgamma_2 = (scalar_t*)(&tgamma_2);                                                 \
    tx                 = *(VecTpye*)(X + index);                                                 \
    tdy                = *(VecTpye*)(dY + index);                                                \
    tgamma             = *(VecTpye*)(gamma + j * VEC);                                           \
    for(int ii = 0; ii < VEC; ii++)                                                              \
    {                                                                                            \
        sum1 += static_cast<T_ACC>(pdy[ii]) * static_cast<T_ACC>(px[ii]) * pgamma[ii];          \
    }                                                                                            \
    if(j < tcol - 512)                                                                           \
    {                                                                                            \
        tx_2     = *(VecTpye*)(X + index_2);                                                     \
        tdy_2    = *(VecTpye*)(dY + index_2);                                                    \
        tgamma_2 = *(VecTpye*)(gamma + j * VEC + 2048);                                          \
        for(int ii = 0; ii < VEC; ii++)                                                          \
        {                                                                                        \
            sum1 += static_cast<T_ACC>(pdy_2[ii]) * static_cast<T_ACC>(px_2[ii]) * pgamma_2[ii]; \
        }                                                                                        \
    }                                                                                            \
    sum1 = aiter::rmsnorm_autograd::BlockReduceSum<T_ACC>(sum1, ds_shared);                        \
    float trstd = rstd[i];                                                                       \
    if(threadIdx.x == 0)                                                                         \
    {                                                                                            \
        const T_ACC s = T_ACC(1) / static_cast<T_ACC>(N);                                        \
        b             = -sum1 * trstd * trstd * trstd * s;                                       \
    }                                                                                            \
    __syncthreads();

#define RMSNORMBACKWARD_VEC                                                                      \
    using T_ACC = at::acc_type<scalar_t, true>;                                                      \
    __shared__ T_ACC ds_shared[16];                                                              \
    __shared__ T_ACC b;                                                                          \
    const int i = blockIdx.x;                                                                    \
    const int j = threadIdx.x;                                                                   \
    using VecTpye = aiter::rmsnorm_autograd::aligned_vector<scalar_t, VEC>;                                       \
    T_ACC sum1 = 0;                                                                              \
    int tcol   = N / VEC;                                                                        \
    VecTpye tx;                                                                                  \
    VecTpye tdy;                                                                                 \
    VecTpye tdx;                                                                                 \
    VecTpye tgamma;                                                                              \
    scalar_t* px    = (scalar_t*)(&tx);                                                          \
    scalar_t* pdy   = (scalar_t*)(&tdy);                                                         \
    scalar_t* pdx   = (scalar_t*)(&tdx);                                                         \
    scalar_t* pgamma = (scalar_t*)(&tgamma);                                                     \
    for(int off = j; off < tcol; off += blockDim.x)                                              \
    {                                                                                            \
        const int index = i * N + off * VEC;                                                     \
        tx              = *(VecTpye*)(X + index);                                                \
        tdy             = *(VecTpye*)(dY + index);                                               \
        tgamma          = *(VecTpye*)(gamma + off * VEC);                                        \
        for(int ii = 0; ii < VEC; ii++)                                                          \
        {                                                                                        \
            sum1 += static_cast<T_ACC>(pdy[ii]) * static_cast<T_ACC>(px[ii]) * pgamma[ii];      \
        }                                                                                        \
    }                                                                                            \
    sum1 = aiter::rmsnorm_autograd::BlockReduceSum<T_ACC>(sum1, ds_shared);                        \
    float trstd = rstd[i];                                                                       \
    if(threadIdx.x == 0)                                                                         \
    {                                                                                            \
        const T_ACC s = T_ACC(1) / static_cast<T_ACC>(N);                                        \
        b             = -sum1 * trstd * trstd * trstd * s;                                       \
    }                                                                                            \
    __syncthreads();
