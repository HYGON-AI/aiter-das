// SPDX-License-Identifier: Apache-2.0 AND MIT
// Copyright (c) 2022-2024, NVIDIA CORPORATION.
// Copyright (C) 2024-2025, Advanced Micro Devices, Inc. All rights reserved.
// Copyright (c) 2026 Hygon Information Technology Co., Ltd.
//
// Histogram/rank selection derives from AITER topk_per_row_kernels.cu.
// Vectorized traversal derives from RAPIDS RAFT select_radix.cuh (Apache-2.0).
// Hygon modifications: FP16 coarse filtering, vectorized ragged output,
// HCU integration, and overflow-count reset. AMD/Hygon contributions use MIT;
// see LICENSE.Apache-2.0 and LICENSE for the applicable terms.
#pragma once

// Internal helper for topk_transform.cu; included inside its anonymous namespace.
// Histogram selection uses aligned vectorized traversal to reduce memory traffic.
// Keep the shared counters and helper boundaries: an otherwise
// equivalent simplification raised SGPR usage from 80 to 112 and regressed HCU
// multi-row throughput. Only the single-block, non-radix-sort path is instantiated.
// FP16 overflow is restarted with a clean selected count before FP32 refinement.

namespace hcu_ragged {
__device__ __forceinline__ auto convert_to_uint32(float x) -> uint32_t {
  uint32_t bits = __float_as_uint(x);
  return (bits & 0x80000000) ? bits : ~bits & 0x7fffffff;
}

template <int step>
static inline __device__ uint32_t extractBinIdx(float x) {
  if constexpr (step == 0) {
    __half hx = __float2half(x);
    uint16_t bits = __half_as_ushort(hx);
    bits = (bits & 0x8000) ? bits : ~bits & 0x7fff;
    return bits >> 5;
  } else {
    uint32_t bits = __float_as_uint(x);
    bits = (bits & 0x80000000) ? bits : ~bits & 0x7fffffff;

    if constexpr (step == 1) {
      return bits >> 21;
    } else if constexpr (step == 2) {
      return (bits >> 10) & 0x7ff;
    } else if constexpr (step == 3) {
      return bits & 0x3ff;
    }
  }
}

template <int shift>
static inline __device__ bool isPartialMatch(float x, uint32_t pattern) {
  if constexpr (shift == 0) {
    return true;
  }
  uint32_t bits = __float_as_uint(x);
  bits = (bits & 0x80000000) ? bits : ~bits & 0x7fffffff;
  return (bits ^ pattern) >> shift == 0;
}

template <typename T, typename idxT, typename Func>
__device__ void vectorized_process(size_t thread_rank, size_t num_threads,
                                   const T* in, idxT len, Func f) {
  using WideT = float4;
  if constexpr (sizeof(T) >= sizeof(WideT)) {
    for (idxT i = thread_rank; i < len; i += num_threads) {
      f(in[i], i);
    }
  } else {
    constexpr int items_per_scalar = sizeof(WideT) / sizeof(T);
    union {
      WideT scalar;
      T array[items_per_scalar];
    } wide;

    int skip_cnt =
        (reinterpret_cast<size_t>(in) % sizeof(WideT))
            ? ((sizeof(WideT) - reinterpret_cast<size_t>(in) % sizeof(WideT)) / sizeof(T))
            : 0;
    if (skip_cnt > len) skip_cnt = len;

    const WideT* in_cast = reinterpret_cast<decltype(in_cast)>(in + skip_cnt);
    const idxT len_cast = (len - skip_cnt) / items_per_scalar;

    for (idxT i = thread_rank; i < len_cast; i += num_threads) {
      wide.scalar = in_cast[i];
      const idxT real_i = skip_cnt + i * items_per_scalar;
#pragma unroll
      for (int j = 0; j < items_per_scalar; ++j) {
        f(wide.array[j], real_i + j);
      }
    }

    if (thread_rank < skip_cnt) f(in[thread_rank], thread_rank);
    const idxT remain_i = skip_cnt + len_cast * items_per_scalar + thread_rank;
    if (remain_i < len) f(in[remain_i], remain_i);
  }
}

template <typename T, typename IdxT, typename Func>
__device__ __forceinline__ void vectorized_write(size_t thread_rank, size_t num_threads,
                                                 T* out, IdxT len, Func f) {
  using WideT = float4;
  if constexpr (sizeof(T) >= sizeof(WideT)) {
    for (IdxT i = thread_rank; i < len; i += num_threads) out[i] = f(i);
  } else {
    constexpr int items_per_scalar = sizeof(WideT) / sizeof(T);
    union {
      WideT scalar;
      T array[items_per_scalar];
    } wide;

    int skip_cnt =
        (reinterpret_cast<size_t>(out) % sizeof(WideT))
            ? ((sizeof(WideT) - reinterpret_cast<size_t>(out) % sizeof(WideT)) / sizeof(T))
            : 0;
    if (skip_cnt > len) skip_cnt = len;

    if (thread_rank < skip_cnt) out[thread_rank] = f(thread_rank);

    WideT* out_cast = reinterpret_cast<decltype(out_cast)>(out + skip_cnt);
    const IdxT len_cast = (len - skip_cnt) / items_per_scalar;

    for (IdxT i = thread_rank; i < len_cast; i += num_threads) {
      const IdxT real_i = skip_cnt + i * items_per_scalar;
#pragma unroll
      for (int j = 0; j < items_per_scalar; ++j) wide.array[j] = f(real_i + j);
      out_cast[i] = wide.scalar;
    }

    const IdxT remain_i = skip_cnt + len_cast * items_per_scalar + thread_rank;
    if (remain_i < len) out[remain_i] = f(remain_i);
  }
}

template <int step, int kNumThreadsPerBlock, int kNumBins, int kNumFinalItems,
          bool multipleBlocksPerRow, bool mergeBlocks, typename SmemFinalType,
          typename SmemOutputType>
__device__ bool processHistogramStep(
    const int* indices, const float* logits, int rowEnd, uint32_t& logitPattern,
    int& thresholdBinIdx, SmemOutputType& smemOutput, int* smemThresholdBinIdx,
    int* smemFinalDstIdx, int* smemFinalBinSize, int* smemFoundTopKValues,
    SmemFinalType& smemFinal, int stride1, int rowStart, int topK) {

#pragma unroll
  for (int idx = threadIdx.x; idx < kNumBins; idx += kNumThreadsPerBlock) {
    smemFinal.histo.data[idx] = 0;
  }
  __syncthreads();

  constexpr auto patternShift = step < 2 ? 0 : step == 2 ? 21 : 10;
  if constexpr (step == 2) {
    logitPattern = static_cast<uint32_t>(thresholdBinIdx & 0x7ff) << patternShift;
  } else if constexpr (step == 3) {
    logitPattern |= static_cast<uint32_t>(thresholdBinIdx & 0x7ff) << patternShift;
  }

  auto distributeToBins = [&](float logit, int /* idx */ = 0) {
    if (isPartialMatch<patternShift>(logit, logitPattern)) {
      uint32_t binIdx = extractBinIdx<step>(logit);
      atomicAdd(&smemFinal.histo.data[binIdx], 1);
    }
  };

  if (stride1 == 1) {
    vectorized_process(threadIdx.x, kNumThreadsPerBlock, logits + rowStart, rowEnd - rowStart, distributeToBins);
  } else {
    for (int idx = rowStart + threadIdx.x; idx < rowEnd; idx += kNumThreadsPerBlock) {
      distributeToBins(logits[idx * stride1], idx);
    }
  }
  __syncthreads();

  int lastValue = smemFoundTopKValues[0];
  constexpr int kItemsPerThread = kNumBins / kNumThreadsPerBlock;
  int binData[kItemsPerThread];

#pragma unroll
  for (int i = 0; i < kItemsPerThread; ++i) {
    binData[i] = smemFinal.histo.data[threadIdx.x * kItemsPerThread + i];
  }

  int prefixSums[kItemsPerThread];
  int totalSum{0};
#ifndef USE_ROCM
  using Scan = cub::BlockScan<int, kNumThreadsPerBlock>;
#else
  using Scan = hipcub::BlockScan<int, kNumThreadsPerBlock>;
#endif
  Scan(smemFinal.histo.scan).ExclusiveSum(binData, prefixSums, totalSum);

#pragma unroll
  for (int i = 0; i < kItemsPerThread; ++i) {
    prefixSums[i] += lastValue;
    smemFinal.histo.data[threadIdx.x * kItemsPerThread + i] = prefixSums[i];
  }
  __syncthreads();

#pragma unroll
  for (int i = 0; i < kItemsPerThread; ++i) {
    int idx = threadIdx.x * kItemsPerThread + i;
    int pSum = prefixSums[i];
    if (pSum < topK) {
      int nextPrefixSum = (idx == kNumBins - 1) ? (totalSum + lastValue) : smemFinal.histo.data[idx + 1];
      if (nextPrefixSum >= topK) {
        smemThresholdBinIdx[0] = idx;
        smemFinalBinSize[0] = nextPrefixSum - pSum;
      }
    }
  }
  __syncthreads();

  thresholdBinIdx = smemThresholdBinIdx[0];

  auto processBins = [&](float logit, int idx) {
    if (isPartialMatch<patternShift>(logit, logitPattern)) {
      uint32_t binIdx = extractBinIdx<step>(logit);
      if (binIdx < thresholdBinIdx) {
        int dstIdx = atomicAdd(&smemFoundTopKValues[0], 1);
        if constexpr (mergeBlocks) {
          smemOutput[dstIdx] = indices[idx];
        } else if constexpr (multipleBlocksPerRow) {
          smemOutput[dstIdx] = idx + rowStart;
          reinterpret_cast<float*>(smemOutput + topK)[dstIdx] = logit;
        } else {
          smemOutput[dstIdx] = idx;
        }
      }
      if constexpr (step < 3) {
        if (binIdx == thresholdBinIdx && smemFinalBinSize[0] <= kNumFinalItems) {
          int dstIdx = atomicAdd(&smemFinalDstIdx[0], 1);
          smemFinal.items.logits[dstIdx] = logit;
          if constexpr (mergeBlocks) {
            smemFinal.items.indices[dstIdx] = indices[idx];
          } else if constexpr (multipleBlocksPerRow) {
            smemFinal.items.indices[dstIdx] = idx + rowStart;
          } else {
            smemFinal.items.indices[dstIdx] = idx;
          }
        }
      } else {
        if (binIdx == thresholdBinIdx) {
          int dstIdx = atomicAdd(&smemFinal.histo.data[binIdx], 1);
          if (dstIdx < topK) {
            if constexpr (mergeBlocks) {
              smemOutput[dstIdx] = indices[idx];
            } else if constexpr (multipleBlocksPerRow) {
              smemOutput[dstIdx] = idx + rowStart;
              reinterpret_cast<float*>(smemOutput + topK)[dstIdx] = logit;
            } else {
              smemOutput[dstIdx] = idx;
            }
          }
        }
      }
    }
  };

  if (stride1 == 1) {
    vectorized_process(threadIdx.x, kNumThreadsPerBlock, logits + rowStart, rowEnd - rowStart, processBins);
  } else {
    for (int idx = rowStart + threadIdx.x; idx < rowEnd; idx += kNumThreadsPerBlock) {
      processBins(logits[idx * stride1], idx);
    }
  }
  __syncthreads();

  return smemFinalBinSize[0] > kNumFinalItems;
}

template <int kNumThreadsPerBlock, int kNumBins, bool useRadixSort,
          bool multipleBlocksPerRow = false, bool mergeBlocks = false,
          int kMaxTopK = 2048,
          typename TransformFunc>
static __device__ void topKPerRowJob(const int* indices, const float* logits,
                                     int rowStart, int rowEnd, int* outIndices,
                                     float* outLogits, int stride1, int topK,
                                     TransformFunc transform_func,
                                     int* outRawIndices = nullptr) {
  static constexpr int kNumFinalItems = kMaxTopK + 512;
  static constexpr int kNumFinalItemsPerThread = kNumFinalItems / kNumThreadsPerBlock;

#ifndef USE_ROCM
  using FinalSort = cub::BlockRadixSort<float, kNumThreadsPerBlock, kNumFinalItemsPerThread, int>;
  using Scan = cub::BlockScan<int, kNumThreadsPerBlock>;
#else
  using FinalSort = hipcub::BlockRadixSort<float, kNumThreadsPerBlock, kNumFinalItemsPerThread, int>;
  using Scan = hipcub::BlockScan<int, kNumThreadsPerBlock>;
#endif

  using FinalSortTempStorage = std::conditional_t<useRadixSort, typename FinalSort::TempStorage, int>;

  struct FinalItems {
    int indices[kNumFinalItems];
    float logits[kNumFinalItems];
  };

  struct Histogram {
    typename Scan::TempStorage scan;
    int data[kNumBins];
  };

  __shared__ union {
    FinalItems items;
    FinalSortTempStorage finalSort;
    Histogram histo;
  } smemFinal;

  extern __shared__ int32_t smemOutput[];

  __shared__ int smemThresholdBinIdx[1];
  __shared__ int smemFinalDstIdx[1];
  __shared__ int smemFinalBinSize[1];
  __shared__ int smemFoundTopKValues[1];

  int rowLen = rowEnd - rowStart;

  // Shortcut if length <= TopK
  if (rowLen <= topK) {
    vectorized_write<int, int>(threadIdx.x, kNumThreadsPerBlock, outIndices, rowLen, [&](int rowIt) {
      int raw_idx = multipleBlocksPerRow ? rowIt + rowStart : rowIt;
      if (outRawIndices != nullptr) {
        outRawIndices[rowIt] = raw_idx;
      }
      return transform_func(raw_idx);
    });

    if constexpr (multipleBlocksPerRow) {
      vectorized_write<float, int>(threadIdx.x, kNumThreadsPerBlock, outLogits, rowLen, [&](int rowIt) {
        return logits[rowIt + rowStart];
      });
    }

    int fillLen = topK - rowLen;
    if (fillLen > 0) {
      vectorized_write<int, int>(threadIdx.x, kNumThreadsPerBlock, outIndices + rowLen, fillLen, [&](int /*i*/) {
        return -1;
      });
      if (outRawIndices != nullptr) {
        vectorized_write<int, int>(threadIdx.x, kNumThreadsPerBlock, outRawIndices + rowLen, fillLen, [&](int /*i*/) {
          return -1;
        });
      }
      if constexpr (multipleBlocksPerRow) {
        vectorized_write<float, int>(threadIdx.x, kNumThreadsPerBlock, outLogits + rowLen, fillLen, [&](int /*i*/) {
          return -FLT_MAX;
        });
      }
    }
    return;
  }

  // The final transform reads every entry in smemOutput. Keep unwritten slots
  // deterministic so an unexpected ranking gap cannot turn stale shared-memory
  // contents into an arbitrary global-memory address.
  vectorized_write<int, int>(
      threadIdx.x, kNumThreadsPerBlock, smemOutput, topK, [&](int /*i*/) {
        return -1;
      });

  if (threadIdx.x == 0) {
    smemFinalDstIdx[0] = 0;
    smemFoundTopKValues[0] = 0;
  }
  __syncthreads();

  int thresholdBinIdx = -1;
  uint32_t logitPattern = 0;

  bool continueToNextStep = processHistogramStep<0, kNumThreadsPerBlock, kNumBins, kNumFinalItems, multipleBlocksPerRow, mergeBlocks>(
      indices, logits, rowEnd, logitPattern, thresholdBinIdx, smemOutput, smemThresholdBinIdx, smemFinalDstIdx, smemFinalBinSize, smemFoundTopKValues, smemFinal, stride1, rowStart, topK);

  if (continueToNextStep) {
    // FP16 coarse selection is superseded by a full-row FP32 rescan. Reset
    // its selected count so previously selected coarse bins are not duplicated.
    if (threadIdx.x == 0) smemFoundTopKValues[0] = 0;
    __syncthreads();
    continueToNextStep = processHistogramStep<1, kNumThreadsPerBlock, kNumBins, kNumFinalItems, multipleBlocksPerRow, mergeBlocks>(
        indices, logits, rowEnd, logitPattern, thresholdBinIdx, smemOutput, smemThresholdBinIdx, smemFinalDstIdx, smemFinalBinSize, smemFoundTopKValues, smemFinal, stride1, rowStart, topK);
  }

  if (continueToNextStep) {
    continueToNextStep = processHistogramStep<2, kNumThreadsPerBlock, kNumBins, kNumFinalItems, multipleBlocksPerRow, mergeBlocks>(
        indices, logits, rowEnd, logitPattern, thresholdBinIdx, smemOutput, smemThresholdBinIdx, smemFinalDstIdx, smemFinalBinSize, smemFoundTopKValues, smemFinal, stride1, rowStart, topK);
  }

  if (continueToNextStep) {
    processHistogramStep<3, kNumThreadsPerBlock, kNumBins, kNumFinalItems, multipleBlocksPerRow, mergeBlocks>(
        indices, logits, rowEnd, logitPattern, thresholdBinIdx, smemOutput, smemThresholdBinIdx, smemFinalDstIdx, smemFinalBinSize, smemFoundTopKValues, smemFinal, stride1, rowStart, topK);
  }

  if (!continueToNextStep) {
    if constexpr (useRadixSort) {
      float finalLogits[kNumFinalItemsPerThread];
      int finalIndices[kNumFinalItemsPerThread];

#pragma unroll
      for (int ii = 0; ii < kNumFinalItemsPerThread; ++ii) finalLogits[ii] = -FLT_MAX;

#pragma unroll
      for (int ii = 0; ii < kNumFinalItemsPerThread; ++ii) {
        int srcIdx = ii * kNumThreadsPerBlock + threadIdx.x;
        if (srcIdx < smemFinalDstIdx[0]) {
          finalLogits[ii] = smemFinal.items.logits[srcIdx];
          finalIndices[ii] = smemFinal.items.indices[srcIdx];
        }
      }
      __syncthreads();

      FinalSort(smemFinal.finalSort).SortDescendingBlockedToStriped(finalLogits, finalIndices);

      int baseIdx = smemFoundTopKValues[0];
#pragma unroll
      for (int ii = 0; ii < kNumFinalItemsPerThread; ++ii) {
        int srcIdx = ii * kNumThreadsPerBlock + threadIdx.x;
        int dstIdx = baseIdx + srcIdx;
        if (dstIdx < topK) {
          smemOutput[dstIdx] = finalIndices[ii];
          if constexpr (multipleBlocksPerRow) {
            reinterpret_cast<float*>(smemOutput + topK)[dstIdx] = finalLogits[ii];
          }
        }
      }
    } else {
      auto baseIdx = smemFoundTopKValues[0];
      for (int i = threadIdx.x; i < smemFinalDstIdx[0]; i += kNumThreadsPerBlock) {
        int outIndex = 0;
        auto logit = smemFinal.items.logits[i];
        auto logitKey = convert_to_uint32(logit);
        for (int j = 0; j < smemFinalDstIdx[0]; j++) {
          auto otherLogit = smemFinal.items.logits[j];
          auto otherLogitKey = convert_to_uint32(otherLogit);
          // Compare total-order integer keys instead of floats. Floating-point
          // comparisons do not order NaNs, which can assign several candidates
          // the same outIndex and leave holes in smemOutput.
          if (logitKey > otherLogitKey || (logitKey == otherLogitKey && i > j)) outIndex++;
        }
        if (outIndex + baseIdx < topK) {
          smemOutput[outIndex + baseIdx] = smemFinal.items.indices[i];
          if constexpr (multipleBlocksPerRow) {
            reinterpret_cast<float*>(smemOutput + topK)[outIndex + baseIdx] = smemFinal.items.logits[i];
          }
        }
      }
    }
    __syncthreads();
  }

  vectorized_write<int, int>(threadIdx.x, kNumThreadsPerBlock, outIndices, topK, [&](int i) {
    if constexpr (multipleBlocksPerRow) {
      return smemOutput[i];
    } else {
      int raw_idx = (stride1 == 1) ? smemOutput[i] : smemOutput[i] - rowStart;
      if (raw_idx < 0 || raw_idx >= rowLen) {
        if (outRawIndices != nullptr) {
          outRawIndices[i] = -1;
        }
        return -1;
      }
      if (outRawIndices != nullptr) {
        outRawIndices[i] = raw_idx;
      }
      return transform_func(raw_idx);
    }
  });

  if constexpr (multipleBlocksPerRow) {
    vectorized_write<float, int>(threadIdx.x, kNumThreadsPerBlock, outLogits, topK, [&](int i) {
      return reinterpret_cast<float*>(smemOutput + topK)[i];
    });
  }
}


template<int Threads>
__global__ __launch_bounds__(Threads) void kernel(FastTopKParams params, int* output, const int* offsets) {
  const int row=blockIdx.x;
  const int start=params.row_starts ? params.row_starts[row] : 0;
  const int length=params.lengths[row];
  const int offset=offsets[row];
  const float* scores=params.input + row * params.input_stride;
  auto transform = [&](int idx) { return idx + offset; };
  topKPerRowJob<Threads, 2048, false>(nullptr, scores, start, start+length,
      output + int64_t(row)*TopK, nullptr, 1, TopK, transform);
}
} // namespace hcu_ragged
